plugins {
    id 'org.springframework.boot' version '2.6.7' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.6.21' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21' apply false
    id 'com.google.cloud.tools.jib' version '3.1.4' apply false
}
subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

    ext {
        set('springCloudVersion', "2021.0.0")
    }

    repositories {
        mavenCentral()
    }

    group = 'org.bk'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'
    dependencies {
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation("org.mockito.kotlin:mockito-kotlin:3.2.0")
        testImplementation 'io.projectreactor:reactor-test'
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "17"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "17"
        }
    }
}
configure([project(':sample-caller'), project(':sample-producer')]) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.google.cloud.tools.jib'

    dependencies {
        implementation(project(":common"))
        implementation('org.springframework.cloud:spring-cloud-starter-sleuth')
        implementation('org.springframework.boot:spring-boot-starter-actuator')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    bootRun {
        args = ["--spring.profiles.active=local"]
    }
//    bootBuildImage {
//        tags = ["latest"]
//    }
    jib {
        from {
            image = 'gcr.io/distroless/java17-debian11'
        }
    }
}


configure([project(':common')]) {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    group = 'org.bk'
    version = '0.0.1-SNAPSHOT'

    dependencies {
        api("org.springframework.boot:spring-boot-starter-webflux")
        api("com.fasterxml.jackson.module:jackson-module-kotlin")
        api("io.projectreactor.kotlin:reactor-kotlin-extensions")
        api("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        api("org.jetbrains.kotlin:kotlin-reflect")
        implementation('com.github.ben-manes.caffeine:caffeine:3.1.0')
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation("org.mockito.kotlin:mockito-kotlin:3.2.0")
        testImplementation('io.projectreactor:reactor-test')
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}
