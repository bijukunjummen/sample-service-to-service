import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.2.5' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.9.23' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23' apply false
    id 'com.google.cloud.tools.jib' version '3.4.2' apply false
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

    java {
        sourceCompatibility = '21'
    }

    ext {
//        set('springCloudVersion', "2021.0.4")
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    group = 'org.bk'
    version = '0.0.2-SNAPSHOT'
    sourceCompatibility = '21'
    dependencies {
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation("org.mockito.kotlin:mockito-kotlin:4.1.0")
        testImplementation 'io.projectreactor:reactor-test'
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            freeCompilerArgs += '-Xjsr305=strict'
            jvmTarget = '21'
        }
    }
}
configure([project(':sample-caller'), project(':sample-producer')]) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.google.cloud.tools.jib'

    dependencies {
        implementation(project(":common"))
        implementation('org.springframework.boot:spring-boot-starter-actuator')
        implementation('io.micrometer:micrometer-tracing-bridge-otel')
        runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    }

    bootRun {
        args = ["--spring.profiles.active=local"]
    }
    jib {
        from {
            image = 'cgr.dev/chainguard/jre:latest'
        }
    }
}


configure([project(':common')]) {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    group = 'org.bk'
    version = '0.0.2-SNAPSHOT'

    dependencies {
        api("org.springframework.boot:spring-boot-starter-webflux")
        api("com.fasterxml.jackson.module:jackson-module-kotlin")
        api("io.projectreactor.kotlin:reactor-kotlin-extensions")
        api("org.jetbrains.kotlin:kotlin-reflect")
        implementation('com.github.ben-manes.caffeine:caffeine:3.1.6')
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation("org.mockito.kotlin:mockito-kotlin:4.1.0")
        testImplementation('io.projectreactor:reactor-test')
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}
