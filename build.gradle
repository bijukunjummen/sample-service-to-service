plugins {
    id 'org.springframework.boot' version '2.5.5' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.5.31' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.5.31' apply false
    id 'com.google.cloud.tools.jib' version '3.1.2' apply false
}
subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

    repositories {
        mavenCentral()
    }

    group = 'org.bk'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'
    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation("org.mockito.kotlin:mockito-kotlin:3.2.0")
        testImplementation 'io.projectreactor:reactor-test'
    }
    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "11"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "11"
        }
    }
}
configure([project(':sample-caller'), project(':sample-producer')]) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'com.google.cloud.tools.jib'

    dependencies {
        implementation(project(":common"))
    }

    bootRun {
        args = ["--spring.profiles.active=local"]
    }
}


configure([project(':common')]) {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    group = 'org.bk'
    version = '0.0.1-SNAPSHOT'

    dependencies {
        api("org.springframework.boot:spring-boot-starter-webflux")
        api("com.fasterxml.jackson.module:jackson-module-kotlin")
        api("io.projectreactor.kotlin:reactor-kotlin-extensions")
        api("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        api("org.jetbrains.kotlin:kotlin-reflect")
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation("org.mockito.kotlin:mockito-kotlin:3.2.0")
        testImplementation 'io.projectreactor:reactor-test'
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}
